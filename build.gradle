buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.3'
    }
}

//common settings for all sub projects
subprojects {
    group 'com.juancavallotti.tools'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.9

    repositories {
        mavenCentral()
    }

    ext {
        //versions of libraries
        logj4version = '2.9.1'
        springBootVersion = '1.5.9.RELEASE'
        slf4jVersion = '1.7.25'
        jacksonVersion = '2.8.10'
        springVersion = '4.3.13.RELEASE'
        commonsIoVersion = '2.6'
        junitVersion = '5.0.3'
    }

    //some shared dependencies.
    dependencies {
        //all modules depend on slf4j-api
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: logj4version
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

        //we are embracing jackson for the modules to customize how they answer
        //this allows less bean to bean mapping work and the api is a JSON api anyways.
        //I just hope I don't have to change jackson to another mapper :)
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion

        //all modules may want some annotations
        compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.2'

        //all modules depend on junit
        testCompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
        testRuntimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: logj4version
        testRuntimeOnly group: 'com.lmax', name: 'disruptor', version: '3.3.7'
        testRuntime project(':test-commons')
    }

    //finally, apply global junit 5 configs.
    junitPlatform {
        filters {
            engines {
                include 'junit-jupiter'
            }
        }
        enableStandardTestTask true
    }

    test {
        useJUnitPlatform()
    }

}




